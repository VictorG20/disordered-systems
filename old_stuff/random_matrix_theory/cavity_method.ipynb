{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "06a16fb9",
   "metadata": {},
   "source": [
    "# Random Matrix Theory - Cavity method\n",
    "\n",
    "Here we obtain the spectral density $\\rho(\\lambda)$ by starting from the following recursive expression for the cavity precisions\n",
    "\n",
    "\\begin{equation}\n",
    "    \\omega^{(j)}_{k} = i(\\lambda - i \\epsilon) + \\sum_{\\ell \\in \\partial k \\backslash j} \\frac{M^{2}_{k \\ell}}{\\omega^{(k)}_{\\ell}}, \\tag{1}\n",
    "\\end{equation}\n",
    "\n",
    "then computing the marginal precisions through the equation\n",
    "\n",
    "\\begin{equation}\n",
    "    \\omega_{j} = i(\\lambda - i \\epsilon) + \\sum_{k \\in \\partial j} \\frac{M^{2}_{k j}}{\\omega^{(j)}_{k}}, \\tag{2}\n",
    "\\end{equation}\n",
    "\n",
    "and finally getting the value of the spectral density via\n",
    "\n",
    "\\begin{equation}\n",
    "    \\rho(\\lambda) = \\lim_{\\epsilon \\to 0^{+}} \\frac{1}{\\pi N} \\sum_{j = 1}^{N} \\text{Im}\\left[\\frac{i}{\\omega_{j}}\\right].  \\tag{3}\n",
    "\\end{equation}\n",
    "\n",
    "\n",
    "## Algorithm\n",
    "\n",
    "### Inputs\n",
    "\n",
    "* Size of the matrix $N$.\n",
    "* Mean connectivity $c$.\n",
    "* Values of $\\lambda$ and $\\epsilon$.\n",
    "* Maximum number of iterations per value of $\\lambda$.\n",
    "* Minimum absolute tolerance, i.e., maximum change allowed to say that we have achieved the fixed point.\n",
    "\n",
    "### Ouputs\n",
    "\n",
    "* Value of $\\rho(\\lambda)$.\n",
    "\n",
    "### Procedure\n",
    "\n",
    "1. Create instance of matrix $\\mathbf{M}$ ($M_{i j} = A_{i j} J_{ij}$). \n",
    "    1. Create a random regular graph of size $N$ and mean connectivity $c$, and extract the adjacency matrix $\\mathbf{A}$.\n",
    "        \n",
    "    1. Define normal distribution from $\\mu$ and $\\sigma$, and from this, draw the elements for the symmetric matrix $\\mathbf{J}$. \n",
    "        \n",
    "    1. Matrix $\\mathbf{M}$ is given by the element-wise product of $\\mathbf{A}$ and $\\mathbf{J}$.\n",
    "    \n",
    "1. To avoid having to access different parts of the matrix $\\mathbf{M}$, create two matrices with the elements of $\\mathbf{M}$ ordered by neighbors, and an array with the positions of the cavity precisions for quick access:\n",
    "\n",
    "    1. A matrix of shape $(c-1) \\times (N \\cdot c)$, that stores the values of $M^{2}_{k \\ell}$ relevant for the recursive equation of each $\\omega^{(j)}_{k}$.\n",
    "    \n",
    "    1. A matrix of shape $c \\times N$ that stores the relevant values of $M^{2}_{k j}$ for the marginal precisions $\\omega_{j}$.\n",
    "    \n",
    "    1. An array with the positions of the relevant cavity precisions $\\omega^{(j)}_{k}$ in the order in which they appear in the recursive equations.\n",
    "\n",
    "1. Update the values of the cavity precisions $\\omega^{(j)}_{k}$.\n",
    "    1. Keep track of the biggest absolute change $\\Delta \\omega$ after each update.\n",
    "    \n",
    "    1. After updating every cavity precision once, check if the maximum change lies below the tolerance. If it doesn't, iterate again; else, finish iterating. Repeat process for a specified maximum of iterations.\n",
    "    \n",
    "1. Compute the marginal precisions.\n",
    "    1. Reshape the cavity precisions to be the same shape as the matrix containing the relevant values of $M$. \n",
    "    1. Perform the element-wise division $M^{2}_{k j} \\; / \\; \\omega^{(j)}_{k}$.\n",
    "    1. Sum along each column.\n",
    "    1. Sum $i(\\lambda - i \\epsilon)$ to every element of the resulting vector.\n",
    "\n",
    "1. Compute the spectral density.\n",
    "    1. For each marginal precision (the resulting vector of the previous step), take the element-wise operation $i / \\omega_{j}$.\n",
    "    1. Sum the entire vector.\n",
    "    1. Take the imaginary part.\n",
    "    1. Divide by $(\\pi N)$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c57ce7ec",
   "metadata": {},
   "source": [
    "## 1. Load packages and define functions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1fbc1f42",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Distributions  # Define normal distributions with given mean and standard deviation.\n",
    "using Random  # Generate random numbers from given distributions.\n",
    "using LightGraphs  # Generate Random Regular Graphs (in particular, adjacency matrices).\n",
    "using LinearAlgebra  # Compute eigenvalues.\n",
    "using ProgressMeter  # Show progress during 'for' loops.\n",
    "using Plots\n",
    "\n",
    "\n",
    "# Generate matrix from ensemble.\n",
    "function make_matrix(N, c; μ=0, σ=1/sqrt(c))\n",
    "    \n",
    "    graph = random_regular_graph(N, c)\n",
    "    A = adjacency_matrix(graph)\n",
    "    \n",
    "    d = Normal(μ, σ)\n",
    "    J = Symmetric(rand(d, (N, N)))\n",
    "    \n",
    "    M = Symmetric(A .* J)\n",
    "    \n",
    "    return M\n",
    "end\n",
    "\n",
    "\n",
    "# Create arrays of M2 values for the solution of the cavity and marginal precisions.\n",
    "function make_recursion_arrays(M)\n",
    "    \n",
    "    c = sum(M[1, :] .!= 0.)\n",
    "    N = size(M)[1]\n",
    "    \n",
    "    M2 = Symmetric(M .* M)  # Element-wise-squared.\n",
    "    \n",
    "    cavity_m = zeros(c-1, N*c)\n",
    "    margin_m = zeros(c, N)\n",
    "    \n",
    "    pos_list = []  # Positions of cavity precisions in array.\n",
    "    \n",
    "    col = 1  # Counter for columns of cavity_m.\n",
    "    \n",
    "    for j ∈ 1:N\n",
    "        \n",
    "        j_neigh = [k for k ∈ 1:N if M[j, k] != 0]\n",
    "        margin_m[:, j] .= M2[j_neigh, j]\n",
    "        \n",
    "        for k ∈ j_neigh\n",
    "            knj_neigh = [l for l in 1:N if M[k, l] != 0 && l != j]\n",
    "            cavity_m[:, col] .= M2[knj_neigh, k]\n",
    "            ω_positions = [(k-1)*c + sum(M[1:l,k] .!= 0) for l in knj_neigh]\n",
    "            \n",
    "            push!(pos_list, ω_positions)\n",
    "            \n",
    "            col += 1\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return cavity_m, margin_m, pos_list\n",
    "end\n",
    "\n",
    "\n",
    "function update_precisions!(ω, z, cavity_m, pos_list)\n",
    "    Δω_max = 0\n",
    "            \n",
    "    for i in 1:N*c\n",
    "        ω_old = ω[i]  # Save value we're updating\n",
    "        ids = pos_list[i]\n",
    "\n",
    "        ω[i] = z + sum(cavity_m[:, i] ./ ω[ids])\n",
    "\n",
    "        Δω = abs(ω[i] - ω_old)\n",
    "        (Δω > Δω_max) && (Δω_max = Δω)\n",
    "    end\n",
    "    \n",
    "    return Δω_max\n",
    "end\n",
    "\n",
    "\n",
    "function cavity_method(M, ϵ, λ_range; tolerance=1e-6, max_iter=1000)\n",
    "    \n",
    "    c = sum(M[1, :] .!= 0.)  # Mean connectivity.\n",
    "    N = size(M)[1]  # Linear size of the matrix M.\n",
    "    \n",
    "    points = length(λ_range)\n",
    "    \n",
    "    cavity_m, margin_m, pos_list = make_recursion_arrays(M)\n",
    "    \n",
    "    ρ_values = zeros(points)\n",
    "    \n",
    "    # Initial values for the cavity precisions.\n",
    "    ω = rand(Complex{Float64}, N*c)\n",
    "    \n",
    "    @showprogress for k ∈ 1:points\n",
    "        \n",
    "        λ = λ_range[k]\n",
    "        z = im*(λ - im*ϵ)\n",
    "        \n",
    "        # Attempt to reach fixed point for cavity precisions.\n",
    "        for iteration in 1:max_iter\n",
    "            \n",
    "            Δω_max = update_precisions!(ω, z, cavity_m, pos_list)\n",
    "            (Δω_max < tolerance) && break\n",
    "            (iteration == max_iter) && println(\"Exhausted iterations at $(λ).\")\n",
    "        end\n",
    "        \n",
    "        # Compute the marginal precisions.\n",
    "        cavity_ω = reshape(ω, (c, N))\n",
    "        margin_ω = z .+ sum(margin_m ./ cavity_ω, dims=1)\n",
    "        \n",
    "        # Compute ρ(λ)\n",
    "        ρ = imag(sum(im ./ margin_ω)) / (π * N)\n",
    "        ρ_values[k] = ρ\n",
    "    end\n",
    "    \n",
    "    return ρ_values\n",
    "end\n",
    "\n",
    "\n",
    "function diago_vs_cavity(N, c, ϵ, λ_range, samples; μ=0, σ=1/sqrt(c), tolerance=1e-6, max_iter=1000)\n",
    "    \n",
    "    λ_values = []\n",
    "    ρ_values = 0\n",
    "    \n",
    "    for i ∈ 1:samples\n",
    "        M = make_matrix(N, c, μ=μ, σ=σ)\n",
    "        \n",
    "        append!(λ_values, eigvals(M))\n",
    "        \n",
    "        if i != 1\n",
    "            ρ_values += cavity_method(M, ϵ, λ_range, tolerance=tolerance, max_iter=max_iter)\n",
    "            continue\n",
    "        end\n",
    "        \n",
    "        ρ_values = cavity_method(M, ϵ, λ_range, tolerance=tolerance, max_iter=max_iter)\n",
    "        \n",
    "    end\n",
    "    \n",
    "    ρ_values = ρ_values/samples\n",
    "    \n",
    "    return λ_values, ρ_values\n",
    "end\n",
    "\n",
    "\n",
    "# Function to create LaTeX ticks based on the maximum values of the plot.\n",
    "function make_latex_ticks(plot)\n",
    "    \n",
    "    # Find maximum absolute values for x and y in the plot. \n",
    "    \n",
    "    x_max = 0\n",
    "    y_max = 0\n",
    "\n",
    "    for series in plot.series_list\n",
    "        x_values = series.plotattributes[:x]\n",
    "        x_maxval = maximum([x for x in x_values if !isnan(x)])\n",
    "        (x_maxval > x_max) && (x_max = x_maxval)\n",
    "\n",
    "        y_values = series.plotattributes[:y]\n",
    "        y_maxval = maximum([y for y in y_values if !isnan(y)])\n",
    "        (y_maxval > y_max) && (y_max = y_maxval)\n",
    "    end\n",
    "    \n",
    "    # Make xticks. ---------------------------------------------\n",
    "\n",
    "    xticks_max = round(Int, x_max)\n",
    "    xticks_range = -xticks_max:xticks_max\n",
    "    xticks_label = [\"\\$ $(i) \\$\" for i ∈ xticks_range];\n",
    "    xticks = (xticks_range, xticks_label)\n",
    "        \n",
    "    # Make yticks. ---------------------------------------------\n",
    "\n",
    "    max_yticks = 6  # Avoid saturating with many ticks.\n",
    "    \n",
    "    if y_max < max_yticks * 0.1\n",
    "        yticks_range = [y for y in 0:0.1:1 if y <= 1.05*y_max]\n",
    "    else\n",
    "        yticks_range = range(0, y_max, length=max_yticks)\n",
    "    end\n",
    "\n",
    "    yticks_label = [\"\\$ $(trunc(y, digits=2)) \\$\" for y in yticks_range]\n",
    "    yticks = (yticks_range, yticks_label)\n",
    "    \n",
    "    return xticks, yticks\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2d45fb90",
   "metadata": {},
   "source": [
    "## 2. Generate data"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bb9000fb",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "Random.seed!(42)  # Fix the seed of every call to random numbers.\n",
    "\n",
    "N = 2^11\n",
    "c = 3\n",
    "ϵ = 0.005\n",
    "\n",
    "samples = 1\n",
    "\n",
    "λ_min = ϵ\n",
    "λ_max = 3.\n",
    "points = 100\n",
    "\n",
    "λ_range = range(λ_min, λ_max, length=points)\n",
    "\n",
    "λ_vals, ρ_vals = diago_vs_cavity(N, c, ϵ, λ_range, samples, tolerance=1e-7);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ad02ee1c",
   "metadata": {},
   "source": [
    "## 3. Make plot"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "363fd5bc",
   "metadata": {},
   "outputs": [],
   "source": [
    "save_figure = true\n",
    "\n",
    "##############################\n",
    "# Prepare data for plotting. #\n",
    "##############################\n",
    "\n",
    "# Make binning for the histogram by centering\n",
    "# the bars at the cavity-method values of λ.\n",
    "\n",
    "Δλ = λ_range.step.hi\n",
    "bin_λ = [-λ - (Δλ/2) for λ in reverse(λ_range)]\n",
    "append!(bin_λ, -reverse(bin_λ))\n",
    "\n",
    "\n",
    "# Complete spectral density values of the cavity \n",
    "# method for negative λ by using ρ(-λ) = ρ(λ).\n",
    "\n",
    "λ_xplot = [-λ for λ in reverse(λ_range)]\n",
    "append!(λ_xplot, λ_range)\n",
    "\n",
    "ρ_yplot = reverse(ρ_vals)\n",
    "append!(ρ_yplot, ρ_vals)\n",
    "\n",
    "\n",
    "####################\n",
    "# Initialize plot. #\n",
    "####################\n",
    "\n",
    "cavity_plot = histogram(λ_vals, label=\"Direct diagonalization\",\n",
    "                        normalize=:pdf, \n",
    "                        bins=bin_λ, \n",
    "                        alpha=0.45, \n",
    "                        color=:aqua)\n",
    "\n",
    "# ρ(λ) via cavity method.\n",
    "plot!(λ_xplot, ρ_yplot, label=\"Cavity method\", color=:blue, lw=1.7)\n",
    "\n",
    "\n",
    "###########################\n",
    "# General plot attributes #\n",
    "###########################\n",
    "\n",
    "kw = (; \n",
    "        xlabel=\"\\$ \\\\lambda \\$\",\n",
    "        xminorgrid=true,\n",
    "        xminorticks=2,\n",
    "        xlabelfontsize=18,\n",
    "    \n",
    "        ylabel=\"\\$ \\\\rho(\\\\lambda) \\$\",\n",
    "        yminorgrid=true,\n",
    "        ylabelfontsize=18,\n",
    "    \n",
    "        tickfontsize=18,\n",
    "        legendfontsize=11,\n",
    "    \n",
    "        framestyle=:box,\n",
    "        size=(600, 400),\n",
    "        dpi=400)\n",
    "\n",
    "plot!(;kw...)\n",
    "\n",
    "xticks, yticks = make_latex_ticks(cavity_plot)\n",
    "\n",
    "plot!(xticks=xticks, yticks=yticks, yminorticks=4)\n",
    "\n",
    "if ! save_figure\n",
    "    cavity_plot\n",
    "else\n",
    "    params_end = \"_N_$(N)_c_$(c)_eps_$(ϵ)_samples_$(samples).png\"\n",
    "    savefig(cavity_plot, \"./Figures/2_2_cavity\" * params_end)\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
